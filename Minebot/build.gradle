buildscript {
    repositories {
        jcenter()
        maven { url = 'https://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'org.ajoberstar:grgit:1.1.0'
    }
}

apply plugin: 'net.minecraftforge.gradle'
compileJava.options.encoding = 'UTF-8'

// VERSION
def git = org.ajoberstar.grgit.Grgit.open(file('..'))
def git_head = git.head()
def git_tags = git.tag.list().findAll {
    it.commit == git_head
}
version = git_tags.size() > 0 ? git_tags[0].getName() : git_head.abbreviatedId;
println "Compiling version ${version}"

group = "de.michaelzangl.minebot"
archivesBaseName = 'Minebot'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: '20200225-1.15.1'

    runs {
        client {
            workingDirectory project.file('run')
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.15.2-31.1.43'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "minebot",
            "Specification-Vendor": "Michael Zangl",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Michael Zangl",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar') 
